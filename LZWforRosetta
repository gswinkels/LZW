parse arg str
default="TOBEORNOTTOBEORTOBEORNOT"
If str='' Then
  str=default
say 'input: ' str
compressed = compress(str)
say compressed
if str=default then
do
  cx='[84, 79, 66, 69, 79, 82, 78, 79, 84, 256, 258, 260, 265, 259, 261, 263]'
  if cx==compressed then say 'compression ok'
end
decompressed = decompress(compressed)
say 'decompressed: ' decompressed
if decompressed=str then say 'decompression ok'
exit

method compress(input = Rexx) private static
uncompressed = input
dict=''
d=''
loop i= 0 to 255
  z=i.d2c
  d[i]=z
  dict[z]=i
end
dict_size=256
res='['
w=''
loop i=1 to uncompressed.length
  c=uncompressed.substr(i,1)
  wc=w||c
  if dict[wc] <> '' then w=wc
  else
    do
      res=res||dict[w]', '
      dict[wc]=dict_size
      dict_size = dict_size + 1
      w=c
    end
end
if w <> '' then res=res||dict[w]', '
return res.left(res.length-2)']'

method decompress(input = Rexx) private static
compressed = input
compressed=compressed.translate('','[],')
compressed=compressed.space
d=''
loop i=0 to 255
  z=i.d2c
  d[i]=z
end
dict_size=256
parse compressed w compressed
res=d[w]
w=d[w]
loop i=1 to compressed.words
  k=compressed.word(i)
  select
    when k=dict_size then do
     entry=w||w.substr(1,1)
    end
    when d[k] <> '' | d[k]= " " then do
      entry=d[k]
    end
    otherwise
      say "bad compressed k: "  k
  end
  res=res||entry
  d[dict_size]=w||entry.substr(1,1)
  dict_size = dict_size + 1
  w=entry
end
return res
